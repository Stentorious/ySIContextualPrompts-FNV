ref rCrosshair

int iType
int iLockState
int iIconAlpha
ref rTemp
string_var sFilename

begin Function {rCrosshair}

	; Special case for placeable water sources
	if eval GetCrosshairWater
		sFilename = "interface\icons\Misc_Flask.dds"

	; Check if ref is valid and assign icon
	elseif eval IsReference rCrosshair

		; Actor icons
		if eval (iType = rCrosshair.GetType) == 42 || iType == 43

			; Dead
			if eval rCrosshair.GetDead
				sFilename = "interface\icons\Misc_Skull.dds"

			; Check if ref has icon assigned via the ySI framework
			elseif eval rCrosshair.ySIGetTrait "tag" != ""
				sFilename = rCrosshair.ySIGetTrait "filename"

			; Assign vendor icon based on service type
			elseif eval rCrosshair.GetMerchantContainer
				if eval GetServiceFlag (rCrosshair.GBO) 131072
					sFilename = "interface\icons\Aid_WeaponRepairKit.dds"
				elseif eval ySIGetTrait "tag" (rTemp = rCrosshair.GetClass) != ""
					sFilename = ySIGetTrait "filename" rTemp
				else
					sFilename = "interface\icons\Misc_BottleCap.dds"
				endif
			endif

		; Container/door icons
		elseif eval iType == 27 || iType == 28

			; Assign vendor icon based on lock state
			if eval (iLockState = rCrosshair.GetLocked) > 0
				if eval rCrosshair.PlayerHasKey
					sFilename = "interface\icons\Misc_Keychain.dds"
				elseif eval rCrosshair.GetLockLevel > 100 || iLockState == 2
					sFilename = "interface\icons\Misc_Key.dds"
				else
					sFilename = "interface\icons\Misc_BobbyPin.dds"
				endif

			; Assign interior/exterior door icons
			elseif eval iType == 28
				sFilename = "interface\icons\Misc_Door.dds"
				if eval rCrosshair.IsLoadDoor && rCrosshair.IsInInterior && IsFormValid (rTemp = rCrosshair.GetTeleportDoor) && rTemp.IsInInterior == 0
					sFilename = "interface\icons\Misc_Door_Ext.dds"
				endif
			elseif eval rCrosshair.ySIGetTrait "tag" != ""
				sFilename = rCrosshair.ySIGetTrait "filename"
			endif

		; Other icons
		elseif eval rCrosshair.ySIGetTrait "tag" != ""
			sFilename = rCrosshair.ySIGetTrait "filename"
		endif
	endif

	; Update icon visibility
	if eval sFilename != ""
		iIconAlpha = 255
		SetUIStringAlt "HUDMainMenu\_ySICP+I" (sFilename)
	endif
	SetUIFloatAlt "HUDMainMenu\_ySICP+A" iIconAlpha

	sv_Destruct sFilename

end