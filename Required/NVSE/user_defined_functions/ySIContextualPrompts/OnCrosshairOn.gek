ref rCrosshair

int iType
int iLockState
int iIconAlpha
ref rClass

begin Function {rCrosshair}

	; Special case for placeable water sources
	if eval GetCrosshairWater
		SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Misc_Flask.dds"
		iIconAlpha = 255

	; Check if ref is valid and assign icon
	elseif eval IsFormValid rCrosshair && IsReference rCrosshair
		iType = rCrosshair.GetType

		; Actor icons
		if eval iType == 42 || iType == 43

			if eval rCrosshair.GetDead
				SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Misc_Skull.dds"
				iIconAlpha = 255

			; Check if ref has icon assigned via the ySI framework
			elseif eval rCrosshair.ySIGetTrait "tag" != ""
				SetUIStringAlt "HUDMainMenu\_ySICP+I" (rCrosshair.ySIGetTrait "filename")
				iIconAlpha = 255

			; Assign vendor icon based on service type
			elseif eval rCrosshair.GetMerchantContainer
				rClass = rCrosshair.GetClass
				if eval GetServiceFlag (rCrosshair.GBO) 131072
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Aid_WeaponRepairKit.dds"
				elseif eval rClass == VendorDrinks
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Aid_Alcohol.dds"
				elseif eval rClass == Doctor
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Aid_DoctorBag.dds"
				elseif eval rClass == VendorWeapons
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Ammo_Bullet.dds"
				elseif eval rClass == VendorFood
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Aid_CookedMeal.dds"
				elseif eval rClass == VendorClothes
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Apparel_Other_Bag.dds"
				elseif eval rClass == VendorChems || rClass == VendorChemsFood
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Aid_Drug.dds"
				else
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Misc_BottleCap.dds"
				endif
				iIconAlpha = 255
			endif

		; Container/door icons
		elseif eval iType == 27 || iType == 28

			; Assign vendor icon based on lock state
			iLockState = rCrosshair.GetLocked
			if iLockState > 0
				if eval rCrosshair.PlayerHasKey
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Misc_Keychain.dds"
				elseif eval rCrosshair.GetLockLevel > 100 || iLockState == 2
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Misc_Key.dds"
				else
					SetUIStringAlt "HUDMainMenu\_ySICP+I" "interface\icons\Misc_BobbyPin.dds"
				endif
				iIconAlpha = 255

			elseif eval rCrosshair.ySIGetTrait "tag" != ""
				SetUIStringAlt "HUDMainMenu\_ySICP+I" (rCrosshair.ySIGetTrait "filename")
				iIconAlpha = 255
			endif

		; Other icons
		elseif eval rCrosshair.ySIGetTrait "tag" != ""
			SetUIStringAlt "HUDMainMenu\_ySICP+I" (rCrosshair.ySIGetTrait "filename")
			iIconAlpha = 255
		endif
	endif

	; Update icon visibility
	SetUIFloatAlt "HUDMainMenu\_ySICP+A" iIconAlpha

end